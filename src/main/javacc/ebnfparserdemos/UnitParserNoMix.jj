/*
 * JScience - Java(TM) Tools and Libraries for the Advancement of Sciences.
 * Copyright (C) 2008-2010 - JScience
 * All rights reserved.
 *
 * Permission to use, copy, modify, and distribute this software is
 * freely granted, provided that this notice is preserved.
 */

options {
  STATIC = false;
  UNICODE_INPUT = true;
  DEBUG_PARSER = false;
  DEBUG_LOOKAHEAD = false;
  DEBUG_TOKEN_MANAGER = false;
  FORCE_LA_CHECK = true;
}

PARSER_BEGIN(UnitParserNoMix)

package ebnfparserdemos;

import tech.units.indriya.format.SymbolMap;
import tech.units.indriya.AbstractUnit;
import javax.measure.Unit;
import javax.measure.Prefix;


/**
 * @author <a href="mailto:eric-r@northwestern.edu">Eric Russell</a>
 * @author  <a href="mailto:jsr275@catmedia.us">Werner Keil</a>
 * @version 1.0.2 ($Revision: 148 $), $Date: 2010-02-18 07:19:01 +0100 (Do, 18 Feb 2010) $
 */
public class UnitParserNoMix {

    private static class Exponent {
        public final int pow;
        public final int root;
        public Exponent (int pow, int root) {
            this.pow = pow;
            this.root = root;
        }
    }

    private SymbolMap symbols;

    public UnitParserNoMix (SymbolMap symbols, java.io.Reader in) {
        this(in);
        this.symbols = symbols;
    }
}

PARSER_END(UnitParserNoMix)

//
// Lexical entities
//

TOKEN: { <#DIGIT: [ "0","1","2","3","4","5","6","7","8","9" ] >
       | <#SUPERSCRIPT_DIGIT: [ "\u2070", "\u00B9", "\u00B2", "\u00B3", "\u2074", "\u2075", "\u2076", "\u2077", "\u2078", "\u2079" ] >
       | <#INITIAL_CHAR: ~["\u0000"-"\u0020", "(", ")", "*", "+", "-", ".", "/", "0"-"9", ":", "^", "\u00B2", "\u00B3", "\u00B7", "\u00B9", "\u2070", "\u2074", "\u2075", "\u2076", "\u2077", "\u2078", "\u2079" ] >
       | <#EXTENDED_CHAR: ~["\u0000"-"\u0020", "(", ")", "*", "+", "-", ".", "/", ":", "^", "\u00B2", "\u00B3", "\u00B7", "\u00B9", "\u2070", "\u2074", "\u2075", "\u2076", "\u2077", "\u2078", "\u2079" ] > }
TOKEN: { <PLUS: "+" > }
TOKEN: { <MINUS: "-" > }
TOKEN: { <ASTERISK: "*" > }
TOKEN: { <MIDDLE_DOT: "\u00B7" > }
TOKEN: { <SOLIDUS: "/" > }
TOKEN: { <CARET: "^" > }
TOKEN: { <OPEN_PAREN: "(" > }
TOKEN: { <CLOSE_PAREN: ")" > }
TOKEN: { <INTEGER: (<DIGIT>)+ > }
TOKEN: { <SUPERSCRIPT_INTEGER: (<SUPERSCRIPT_DIGIT>)+ > }
TOKEN: { <FLOATING_POINT: (<DIGIT>)* (".")? (<DIGIT>)+ (("e" | "E") ((<PLUS>)|(<MINUS>))? (<DIGIT>)+)? > }
TOKEN: { <LOG: "l" "o" "g" >
       | <NAT_LOG: "l" "n" >
       | <E: "e" > }
TOKEN: { <UNIT_IDENTIFIER: <INITIAL_CHAR> (<EXTENDED_CHAR>)* > }

//
// Parser productions
//

public Unit<?> parseUnit () :
    {
        Unit<?> result;
    }
{
    result=addExpr() <EOF>
    {
        return result;
    }
}


Unit<?> addExpr () :
    {
        Unit<?> result = AbstractUnit.ONE;
        Number n1 = null;
        Token sign1 = null;
        Number n2 = null;
        Token sign2 = null;
    }
{
(
    ( LOOKAHEAD(numberExpr() sign()) n1=numberExpr() sign1=sign() )?
    result=mulExpr()
    ( sign2=sign() n2=numberExpr() )?
    {
        if (n1 != null) {
            if (sign1.image.equals("-")) {
                result = result.multiply(-1);
            }
            result = result.shift(n1.doubleValue());
        }
        if (n2 != null) {
            double offset = n2.doubleValue();
            if (sign2.image.equals("-")) {
                offset = -offset;
            }
            result = result.shift(offset);
        }
        return result;
    }
)
}

Unit<?> mulExpr () :
    {
        Unit<?> result = AbstractUnit.ONE;
        Unit<?> temp = AbstractUnit.ONE;
    }
{
(
    result=exponentExpr()
    (
      ( (<ASTERISK> | <MIDDLE_DOT>) temp=exponentExpr() { result=result.multiply(temp); } )
    |
      ( <SOLIDUS> temp=exponentExpr() { result=result.divide(temp); } )
    )*
    { return result; }
)
}

Unit<?> exponentExpr () :
    {
        Unit<?> result = AbstractUnit.ONE;
        Unit<?> temp = AbstractUnit.ONE;
        Exponent exponent = null;
        Token token = null;
    }
{
(
    LOOKAHEAD((<INTEGER> | <E>) <CARET>)
    ( ((token=<INTEGER>) | token=<E>) <CARET> result=atomicExpr() )
    {
        double base;
        if (token.kind == INTEGER) {
            base = Integer.parseInt(token.image);
        } else {
            base = StrictMath.E;
        }
        return result.transform(new tech.units.indriya.function.LogConverter(base).inverse());
    }
|
    ( result=atomicExpr() ( exponent=exp() )? )
    {
        if (exponent != null) {
            if (exponent.pow != 1) {
                result = result.pow(exponent.pow);
            }
            if (exponent.root != 1) {
                result = result.root(exponent.root);
            }
        }
        return result;
    }
|
    ( ( ( <LOG> (token=<INTEGER>)? ) | token=<NAT_LOG> ) <OPEN_PAREN> result=addExpr() <CLOSE_PAREN> )
    {
        double base = 10;
        if (token != null) {
            if (token.kind == INTEGER) {
                base = Integer.parseInt(token.image);
            } else if (token.kind == NAT_LOG) {
                base = StrictMath.E;
            }
        }
        return result.transform(new tech.units.indriya.function.LogConverter(base));
    }
)
}

Unit<?> atomicExpr () :
    {
        Unit<?> result = AbstractUnit.ONE;
        Unit<?> temp = AbstractUnit.ONE;
        Number n = null;
        Token token = null;
    }
{
(
    ( n=numberExpr() )
    {
        if (n instanceof Integer) {
            return result.multiply(n.intValue());
        } else {
            return result.multiply(n.doubleValue());
        }
    }
|
    ( token=<UNIT_IDENTIFIER> )
    {
        Unit<?> unit = symbols.getUnit(token.image);
        if (unit == null) {
            Prefix prefix = symbols.getPrefix(token.image);
            if (prefix != null) {
                String prefixSymbol = symbols.getSymbol(prefix);
                unit = symbols.getUnit(token.image.substring(prefixSymbol.length()));
                if (unit != null) {
                    return unit.prefix(prefix);
                }
            }
            throw new ParseException();
        } else {
            return unit;
        }
    }
|
    ( <OPEN_PAREN> result=addExpr() <CLOSE_PAREN> )
    {
        return result;
    }
)
}

Token sign () :
    {
        Token result = null;
    }
{
(
    (result=<PLUS>)
|
    (result=<MINUS>)
)
    {
        return result;
    }
}

Number numberExpr () :
    {
        Token token = null;
    }
{
(
    ( token=<INTEGER> )
    {
        return Long.valueOf(token.image);
    }
|
    ( token=<FLOATING_POINT> )
    {
        return Double.valueOf(token.image);
    }
)
}

Exponent exp () :
    {
        Token powSign = null;
        Token powToken = null;
        Token rootSign = null;
        Token rootToken = null;
    }
{
(
    (
      <CARET>
      (
        ( (powSign=sign())? powToken=<INTEGER> )
        {
            int pow = Integer.parseInt(powToken.image);
            if ((powSign != null) && powSign.image.equals("-")) {
                pow = -pow;
            }
            return new Exponent(pow, 1);
        }
      |
        ( <OPEN_PAREN> (powSign=sign())? powToken=<INTEGER> ( <SOLIDUS> (rootSign=sign())? rootToken=<INTEGER> )? <CLOSE_PAREN> )
        {
            int pow = Integer.parseInt(powToken.image);
            if ((powSign != null) && powSign.image.equals("-")) {
                pow = -pow;
            }
            int root = 1;
            if (rootToken != null) {
                root = Integer.parseInt(rootToken.image);
                if ((rootSign != null) && rootSign.image.equals("-")) {
                    root = -root;
                }
            }
            return new Exponent(pow, root);
        }
      )
    )
|
    ( powToken=<SUPERSCRIPT_INTEGER> )
    {
        int pow = 0;
        for (int i = 0; i < powToken.image.length(); i += 1) {
            pow *= 10;
            switch (powToken.image.charAt(i)) {
                case '\u00B9': pow += 1; break;
                case '\u00B2': pow += 2; break;
                case '\u00B3': pow += 3; break;
                case '\u2074': pow += 4; break;
                case '\u2075': pow += 5; break;
                case '\u2076': pow += 6; break;
                case '\u2077': pow += 7; break;
                case '\u2078': pow += 8; break;
                case '\u2079': pow += 9; break;
            }
        }
        return new Exponent(pow, 1);
    }
)
}
